## 题目

**练习 2.73**：2.3.2 节描述了一个执行符号求导的程序：

```scheme
(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp) 
         (if (same-variable? exp var) 1 0))
        ((sum? exp)
         (make-sum (deriv (addend exp) var)
                   (deriv (augend exp) var)))
        ((product? exp)
         (make-sum
           (make-product 
            (multiplier exp)
            (deriv (multiplicand exp) var))
           (make-product 
            (deriv (multiplier exp) var)
            (multiplicand exp))))
        ⟨更多规则可以加在这里⟩
        (else (error "unknown expression type:
                      DERIV" exp))))
```

可以认为，这个程序是在执行一种基于被求导表达式类型的分派工作。在这里，数据的“类型标志”就是代数运算符（例如 `+`），需要执行的操作是 `deriv`。我们也可以将这一程序变换到数据导向的风格，将基本求导过程重新写成：

```scheme
(define (deriv exp var)
   (cond ((number? exp) 0)
         ((variable? exp) 
           (if (same-variable? exp var) 
               1 
               0))
         (else ((get 'deriv (operator exp)) 
                (operands exp) 
                var))))

(define (operator exp) (car exp))
(define (operands exp) (cdr exp))
```

- a）请解释上面究竟做了些上面。为什么我们无法将相近的谓词 `number?` 和 `same-variable?` 也加入数据导向分派中？
- b）请写出针对和式与积式的求导过程，并把它们安装到表格里，以便上面程序使用所需要的辅助性代码。
- c）请选择一些你希望包括的求导规则，例如对乘幂（练习 2.56）求导等等，并将它们安装到这一数据导向的系统里。
- d）在这一简单的代数运算器中，表达式的类型就是构造起它们来的代数运算符。假定我们想以另一种相反的方式做索引，使得 `deriv` 里完成分派的代码行像下面这样：

```scheme
((get (operator exp) 'deriv) 
 (operands exp) var)
```

求导系统里还需要做哪些相应的改动？

## a

题目的意思是，有这么一张表：

| 类型标签 / 操作 | deriv      |
| --------- | ---------- |
| +         | <对和式求导的过程> |
| *         | <对乘式求导的过程> |

在新版本中，我们使用 `(get 'deriv (operator exp))` 来获取与**操作符**对应的求导规则。这样，我们可以根据操作符来动态选择执行哪个规则。

谓词 `number?` 和 `same-variable?` 用于判断表达式的属性，而不是操作符。在数据导向的风格中，我们需要一个操作符来决定执行哪个规则。因此，只有与操作符相关的谓词才能被有效地加入数据导向分派中。

## b

`common.rkt`：

```scheme
(define (variable? x) (symbol? x))

(define (same-variable? v1 v2)
  (and (variable? v1)
       (variable? v2)
       (eq? v1 v2)))

(define (=number? exp num)
  (and (number? exp) (= exp num)))

(define (make-sum a1 a2)
  (cond ((=number? a1 0) a2)
        ((=number? a2 0) a1)
        ((and (number? a1) (number? a2)) 
         (+ a1 a2))
        (else (list '+ a1 a2))))

(define (make-product m1 m2)
  (cond ((or (=number? m1 0) 
             (=number? m2 0)) 
         0)
        ((=number? m1 1) m2)
        ((=number? m2 1) m1)
        ((and (number? m1) (number? m2)) 
         (* m1 m2))
        (else (list '* m1 m2))))
```

`sum_pkg.rkt`：

```scheme
(load "common.rkt")
(load "make_table.rkt")

(define (install-sum-package)
  ;; 内部过程
  (define (addend s) (car s))
  (define (augend s) (cadr s))
  (define (sum-deriv exp var)
    (make-sum (deriv (addend exp) var)
              (deriv (augend exp) var)))
  ;; 对外接口
  (put 'deriv '+ sum-deriv)
  'done)
```

`product_pkg.rkt`：

```scheme
(load "common.rkt")
(load "make_table.rkt")

(define (install-product-package)
  ;; 内部过程
  (define (multiplier p) (car p))
  (define (multiplicand p) (cadr p))
  (define (product-deriv exp var)
    (make-sum
     (make-product 
      (multiplier exp)
      (deriv (multiplicand exp) var))
     (make-product 
      (deriv (multiplier exp) var)
      (multiplicand exp))))
  ;; 对外接口
  (put 'deriv '* product-deriv)
  'done)
```

`ex2.73.rkt`：

```scheme
(load "make_table.rkt")
(load "sum_pkg.rkt")
(load "product_pkg.rkt")

(define (operator exp) (car exp))
(define (operands exp) (cdr exp))

(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp) 
         (if (same-variable? exp var) 
             1 
             0))
        (else ((get 'deriv (operator exp)) 
               (operands exp)
               var))))

(install-sum-package)
(install-product-package)
```

## c

`exp_pkg.rkt`：

```scheme
(load "common.rkt")
(load "make_table.rkt")

(define (install-exponentiation-package)
  ;; 内部过程
  ;; 选择函数
  (define base car) 
  (define exponent cadr) 
  ;; 构造函数
  (define (make-exponentiation base exponent) 
    (cond ((=number? base 0) 0) 
          ((=number? exponent 1) base) 
          ((=number? exponent 0) 1) 
          ((and (number? base) (number? exponent))  
           (expt base exponent)) 
          (else (list '** base exponent))))
  ;; 求导过程
  (define (exp-deriv operands var) 
    (make-product (exponent operands) 
                  (make-product (make-exponentiation (base operands) 
                                                     (- (exponent operands) 1)) 
                                (deriv (base operands) var))))
  ;; 对外接口
  (put 'deriv '** exp-deriv)
  'done)
```

在 `ex2.73.rkt` 中添加 `(load "exp_pkg.rkt")` 和 `(install-exponentiation-package)` 。

## d

每个包里调用 `put` 时的参数列表需要调整。

比如原本的：

```scheme
(put 'deriv '* product-deriv)
```

现在要改成：

```scheme
(put '* 'deriv product-deriv)
```